
apply plugin: 'java'

group 'com.apress.isf'
version '1.0'


repositories {
    mavenCentral()
}

ext {
    slf4jVersion = '1.7.5'
    logbackVersion = '1.0.13'
}

List loggers = [
        "org.slf4j:slf4j-api:${slf4jVersion}",
        "org.slf4j:jcl-over-slf4j:${slf4jVersion}",
        "org.slf4j:log4j-over-slf4j:${slf4jVersion}",
        "org.slf4j:jul-to-slf4j:${slf4jVersion}",
        "ch.qos.logback:logback-core:${logbackVersion}",
        "ch.qos.logback:logback-classic:${logbackVersion}"
]

dependencies {
    compile("org.springframework:spring-context:4.0.6.RELEASE")
    compile("org.codehaus.groovy:groovy-all:2.3.6")
    compile loggers
    testCompile("junit:junit:4.11")
}

configurations {
    all.collect { configuration ->
        configuration.exclude group: 'commons-logging', module: 'commons-logging'
        configuration.exclude group: 'log4j', module: 'log4j'
        configuration.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        configuration.exclude group: 'org.slf4j', module: 'slf4j-jcl'
        configuration.exclude group: 'org.slf4j', module: 'slf4j-jdk14'
    }
}

test {
    testLogging {
        events 'started', 'passed'
    }
}
